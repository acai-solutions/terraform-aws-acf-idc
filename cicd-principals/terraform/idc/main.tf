# ACAI Cloud Foundation (ACF)
# Copyright (C) 2025 ACAI GmbH
# Licensed under AGPL v3
#
# This file is part of ACAI ACF.
# Visit https://www.acai.gmbh or https://docs.acai.gmbh for more information.
#
# For full license text, see LICENSE file in repository root.
# For commercial licensing, contact: contact@acai.gmbh


# ---------------------------------------------------------------------------------------------------------------------
# ¦ VERSIONS
# ---------------------------------------------------------------------------------------------------------------------
terraform {
  required_version = ">= 1.3.10"

  required_providers {
    aws = {
      source                = "hashicorp/aws"
      version               = ">= 5.30"
      configuration_aliases = []
    }
  }
}

data "aws_partition" "current" {}

# ---------------------------------------------------------------------------------------------------------------------
# ¦ IAM ROLE - IDC PROVISIONER (org-mgmt / IdC delegated admin)
# ---------------------------------------------------------------------------------------------------------------------
resource "aws_iam_role" "cicd_principal" {
  name                 = var.iam_role_settings.name
  path                 = var.iam_role_settings.path
  permissions_boundary = var.iam_role_settings.permissions_boundary_arn
  description          = "IAM Role used to provision AWS Identity Center resources"
  assume_role_policy   = data.aws_iam_policy_document.assume_role_policy.json
  tags                 = var.resource_tags
}

data "aws_iam_policy_document" "assume_role_policy" {
  statement {
    effect  = "Allow"
    actions = ["sts:AssumeRole"]
    principals {
      type        = "AWS"
      identifiers = var.iam_role_settings.aws_trustee_arns
    }
  }
}

resource "aws_iam_role_policy" "idc" {
  name   = "IdentityCenterProvisioning"
  role   = aws_iam_role.cicd_principal.id
  policy = data.aws_iam_policy_document.idc.json
}

#tfsec:ignore:AVD-AWS-0057
data "aws_iam_policy_document" "idc" {
  #checkov:skip=CKV_AWS_111
  #checkov:skip=CKV_AWS_356
  #checkov:skip=CKV_AWS_109
  statement {
    sid    = "ReadPermissions"
    effect = "Allow"
    actions = [
      "sso:Describe*",
      "sso:Get*",
      "sso:List*",
      "sso:TagResource",
      "identitystore:Describe*",
      "identitystore:Get*",
      "identitystore:List*",
    ]
    resources = ["*"]
  }
  statement {
    sid    = "PermissionSetProvisioning"
    effect = "Allow"
    actions = [
      "sso:*PermissionSet*",
    ]
    resources = ["*"]
  }
  statement {
    sid    = "AccountAssignment"
    effect = "Allow"
    actions = [
      "sso:*AccountAssignment*",
    ]
    resources = ["*"]
  }
  statement {
    sid    = "OrganizationsRead"
    effect = "Allow"
    actions = [
      "organizations:Describe*",
      "organizations:List*",
    ]
    resources = ["*"]
  }
  statement {
    sid    = "IAMListPermissions"
    effect = "Allow"
    actions = [
      "iam:List*",
    ]
    resources = ["*"]
  }
  statement {
    sid    = "GetSAMLProvider"
    effect = "Allow"
    actions = [
      "iam:GetSAMLProvider",
      "iam:CreateSAMLProvider",
      "iam:UpdateSAMLProvider",
    ]
    resources = ["arn:${data.aws_partition.current.partition}:iam::*:saml-provider/AWSSSO_*_DO_NOT_DELETE"]
  }
  statement {
    sid    = "AccessToSSOProvisionedRoles"
    effect = "Allow"
    actions = [
      "iam:AttachRolePolicy",
      "iam:CreateRole",
      "iam:DeleteRole",
      "iam:DeleteRolePolicy",
      "iam:GetRole",
      "iam:ListAttachedRolePolicies",
      "iam:ListRolePolicies",
      "iam:PutRolePolicy",
      "iam:UpdateRole",
      "iam:UpdateRoleDescription",
    ]
    resources = ["arn:${data.aws_partition.current.partition}:iam::*:role/aws-reserved/sso.amazonaws.com/*"]
  }
  statement {
    sid    = "ReportingCrawlerRoleManagement"
    effect = "Allow"
    actions = [
      "iam:GetRole",
      "iam:GetRolePolicy",
      "iam:ListRolePolicies",
      "iam:ListAttachedRolePolicies",
      "iam:CreateRole",
      "iam:DeleteRole",
      "iam:UpdateRole",
      "iam:UpdateAssumeRolePolicy",
      "iam:PutRolePolicy",
      "iam:DeleteRolePolicy",
      "iam:AttachRolePolicy",
      "iam:DetachRolePolicy",
      "iam:TagRole",
      "iam:UntagRole",
      "iam:PassRole",
    ]
    resources = ["*"]
  }
}
